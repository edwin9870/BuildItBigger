apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        free {
            applicationIdSuffix ".free"
            buildConfigField 'boolean', 'IS_FREE_FLAVOR', 'true'
        }

        paid {
            applicationIdSuffix ".full"
            buildConfigField 'boolean', 'IS_FREE_FLAVOR', 'false'
        }
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })
    compile 'com.android.support.test.espresso:espresso-idling-resource:2.2.2'
    compile project(":jokeprovider")
    compile project(":jokesdisplayer")
    compile 'com.android.support:appcompat-v7:25.3.1'
    freeCompile 'com.google.android.gms:play-services-ads:11.0.0'
    compile project(path: ':jokes_backend', configuration: 'android-endpoints')
    compile('com.google.api-client:google-api-client-android:1.22.0') {
        exclude module: 'httpclient'
    }

}

task runAndroidTest{
    dependsOn "connectedAndroidTest"
}


Task appEngineRun = tasks.getByPath(":jokes_backend:appengineRun")
Task appEngineStop = tasks.getByPath(":jokes_backend:appengineStop")

task fullTest {
    dependsOn appEngineRun
    dependsOn appEngineStop
    dependsOn "runAndroidTest"
    doLast {
        println "Full test executed!"
    }
}

runAndroidTest.mustRunAfter appEngineRun
appEngineStop.mustRunAfter runAndroidTest




